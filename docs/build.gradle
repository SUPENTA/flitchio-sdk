apply from: 'checkSetup.gradle'

ext {
    srcDir = 'src'
    outputDir = 'build'
    outputDocsDir = outputDir + '/docs'
    outputSpellingDir = outputDir + '/spelling'
}

task("clean") {
    delete(outputDir)
}

/*
 * SPELL CHECK
 */
task("checkSpelling") {
    dependsOn "checkSetupSphinx",
            "checkSetupSphinxSpelling"

    inputs.dir fileTree(dir: srcDir, include: '*.rst')
    outputs.dir outputSpellingDir
} << {
    exec {
        commandLine 'sphinx-build',
                '-b', 'spelling',
                srcDir,
                outputSpellingDir

        standardOutput = new ByteArrayOutputStream()

        ignoreExitValue = true // otherwise the misspelled words make it fail
    }

    System.err << "WARNING: the following words are not correctly spelled:\n"
    System.err << file("$outputSpellingDir/output.txt").text
}

/*
 * SPHINX -> HTML
 */
task("buildHtml") {
    dependsOn "checkSetupSphinx",
            "checkSetupSphinxJavalink",
            "checkSetupSphinxAnalytics",
            ":sdk:assembleRelease", // To generate classes for sphinx-javalink
            ":sdk:javadoc"
    // Not exactly necessary to build, but it is extremely misleading without the latest Javadoc

    inputs.dir srcDir
    inputs.property("version", libraryVersion)
    outputs.dir outputDocsDir
} << {
    exec {
        workingDir srcDir

        commandLine 'sphinx-build',
                '-b', 'html',
                '-D', 'version=' + libraryVersion,
                '-D', 'release=' + libraryVersion,
                '.',
                '../' + outputDocsDir

        standardOutput = new ByteArrayOutputStream()
    }
}
